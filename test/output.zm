/******************************************************************************\
FILE:           output.zm
AUTHOR:         Theo Veenker (UiL-OTS) <T.J.G.Veenker@uu.nl>
ADAPTED BY:     -

DESCRIPTION:

Defines the formats of output table(s).


HISTORY:
2012-11-10 TV   Created.

\******************************************************************************/

enum ExpPhase {
    FAM_PHASE,  // familiarization phase
    TEST_PHASE  // test phase.
};


// Test phase output table record, defining what is to be logged for
// each trial.
record TestOutput
{

    ExpPhase    phase;          // In which phase we are.
    int         trialnum;       // trial number

    int         number;         // item number
    ItemType    type;           // item type

    int         cumlt;          // cumulative looking time [ms]
    int         cumnlt;         // cumulative not-looking time [ms]
    bool        lookedaway;     // if set, infant looked away too long

    dur         cumlt_s1;       // cumulative looking time over trials [ms]
    dur         cumlt_s2;       // cumulative looking time over trials [ms]

    int         nlooks;         // Number of looks on target
    int         nlookaways;     // Number of times the child looked away
}

// Familiarization output table for eyemovement and manual responses
TestOutput[..] fam_man_output;
TestOutput[..] fam_eye_output;

// Test phase output table for eyemovement and manual responses.
TestOutput[..] test_man_output;
TestOutput[..] test_eye_output;

//------------------------------------------------------------------------------

// Appends one row of data to a output table.
void output_append(TestOutput[] output, TestOutput data)
{
    output.size = output.size + 1;
    output[output.size-1] = data;
}


// Saves familiarization phase output table as a CSV file and clears the table.
void fam_output_save()
{
    // Save output table as CSV file. The filename format is:
    //   <dbname>/<dbnum>/<expname>/ses<sesid>-<tblnum>.csv
    string fn = expdb.experiment.session.create_table("fam_manual",1);
    fam_man_output.save(fn, "Familiarization phase results eye");
    fam_man_output.size = 0;
    
    fn = expdb.experiment.session.create_table("fam_eye",2);
    fam_eye_output.save(fn, "Familiarization phase results man");
    fam_eye_output.size = 0;
}

// Saves test phase output table as a CSV file and clears the table.
void test_output_save()
{
    // Save output table as CSV file. The filename format is:
    //   <dbname>/<dbnum>/<expname>/ses<sesid>-<tblnum>.csv
    string fn = expdb.experiment.session.create_table("test_manual", 3);
    test_man_output.save(fn, "Test phase results");
    test_man_output.size = 0;
    
    fn = expdb.experiment.session.create_table("test_eye", 4);
    test_eye_output.save(fn, "Test phase results");
    test_eye_output.size = 0;
}
